module uart_rx (
    input  wire       clk,
    input  wire       rst_n,
    input  wire       rx_serial,
    output reg [7:0]  rx_data,
    output reg        rx_done
);

    wire baud_tick;

    baud_gen baud_inst (
        .clk(clk),
        .rst_n(rst_n),
        .baud_tick(baud_tick)
    );

    localparam IDLE  = 2'd0,
               START = 2'd1,
               DATA  = 2'd2,
               STOP  = 2'd3;

    reg [1:0] state;
    reg [2:0] bit_cnt;
    reg [7:0] data_reg;

    always @(posedge clk or negedge rst_n) begin
        if (!rst_n) begin
            state    <= IDLE;
            bit_cnt  <= 0;
            data_reg <= 0;
            rx_data  <= 0;
            rx_done  <= 0;
        end
        else begin
            rx_done <= 0;  // default

            case (state)

                IDLE: begin
                    if (rx_serial == 0) begin  // detect start bit
                        state <= START;
                    end
                end

                START: begin
                    if (baud_tick) begin
                        bit_cnt <= 0;
                        state   <= DATA;
                    end
                end

                DATA: begin
                    if (baud_tick) begin
                        data_reg[bit_cnt] <= rx_serial;

                        if (bit_cnt == 3'd7)
                            state <= STOP;
                        else
                            bit_cnt <= bit_cnt + 1;
                    end
                end

                STOP: begin
                    if (baud_tick) begin
                        if (rx_serial == 1) begin
                            rx_data <= data_reg;
                            rx_done <= 1;
                        end
                        state <= IDLE;
                    end
                end

            endcase
        end
    end

endmodule
